# Snakefile to run full data analysis with any set of constrasts
tpSize = "100000"
gpSize = "300000"
CHR =["22"]
for i in range(1, 23):
  CHR.append(str(i))
SUBDATASET = []
for i in range(1, 2):
  SUBDATASET.append("tp" + str(i) + "_" + tpSize)
GWAS = ["gp" + str(gpSize)]
CONTRASTS = ["sharp", "north", "east"]
PC = [0, 10]
COVAR = ["no-FGr", "FGr-LOCO"]
PHENOTYPE=["StandingHeight_Normalized_50"]
PVALUE_THRESHOLD = "1e-5"

#wildcard_constraints:
#    subdataset="tp*",
#    gwas="gp*"

def get_chr_num(x):
  out = int(x)
  return out

def get_params(x):
  out = x.split("_")[1]
  return out

rule all:
    input:
        expand("plots/AA/results.txt", chr=CHR, subdataset=SUBDATASET, gwas=GWAS, phenotype=PHENOTYPE, covar=COVAR, contrasts=CONTRASTS, pc=PC)


## Get final set of SNPs

rule get_overlapping_snps:
    input:
        ALL="data/ukbb/variants/ALL/ukb_imp_chr{chr}_v3.afreq",
        WBS="data/ukbb/variants/WBS/ukb_imp_chr{chr}_v3.afreq"
    output:
        "data/AA/variants/overlappingSNPs_chr{chr}.txt"
    shell:
        """
        Rscript code/hgdp/overlapping_snps.R {input.ALL} {input.WBS} {output}
        """

## Get lists of IDs

rule get_ID_lists:
    input:
        sex="data/phenotypes/genetic_sex_22001.txt",
        batch="data/phenotypes/genotype_measurement_batch_22000.txt",
        north="data/phenotypes/PlaceOfBirthNorthCord_129.txt",
        east="data/phenotypes/PlaceOfBirthEastCord_130.txt",
        age="data/phenotypes/age_at_recruitment_21022.txt",
        genotyped="/scratch/jgblanc/ukbb/plink2-files/ALL/ukb_imp_chr22_v3.psam",
        wbs="data/ukbb/WBS.ids"
    output:
        gwas="data/AA/ids/{subdataset}/{gwas}/gwas.ids",
        test="data/AA/ids/{subdataset}/{gwas}/test.ids"
    params:
        gs=int(gpSize),
        ts=int(tpSize)
    shell:
        """
        Rscript code/AA/get_IDs.R {input.sex} {input.batch} {input.north} {input.east} {input.age} {input.genotyped} {output.gwas} {output.test} {params.gs} {params.ts} {input.wbs}
        """

## Get test vector contrasts

rule get_TestVector:
    input:
        North="data/phenotypes/PlaceOfBirthNorthCord_129.txt",
        East = "data/phenotypes/PlaceOfBirthEastCord_130.txt",
        ids="data/AA/ids/{subdataset}/{gwas}/test.ids"
    output:
        north = "data/AA/TestVecs/{subdataset}/{gwas}/north.txt",
        east = "data/AA/TestVecs/{subdataset}/{gwas}/east.txt",
        sharp = "data/AA/TestVecs/{subdataset}/{gwas}/sharp.txt"
    shell:
        """
        Rscript code/AA/get_Tvec.R {input.North} {input.East} {input.ids} {output.north} {output.east} {output.sharp}
        """

rule compute_r:
    input:
        Tvec="data/AA/TestVecs/{subdataset}/{gwas}/{contrasts}.txt",
        IDs="data/AA/ids/{subdataset}/{gwas}/test.ids",
        snps="data/AA/variants/overlappingSNPs_chr{chr}.txt"
    output:
        "data/AA/r/{subdataset}/{gwas}/{contrasts}_chr{chr}.rvec"
    resources:
        mem_mb=50000,
	time="03:00:00"
    threads: 16
    params:
        prefix_tp = "/scratch/jgblanc/ukbb/plink2-files/ALL/ukb_imp_chr{chr}_v3",
        prefix_out = "data/AA/r/{subdataset}/{gwas}/{chr}-{contrasts}"
    shell:
        """
        Rscript code/AA/compute_r.R {params.prefix_tp} {input.Tvec} {params.prefix_out} {input.snps} {output} {input.IDs}
        rm {params.prefix_out}xt_temp.*
        """

## Compute FGr

rule compute_FGr_chr:
    input:
        r="data/AA/r/{subdataset}/{gwas}/{contrasts}_chr{chr}.rvec",
        gp_genos="/scratch/jgblanc/ukbb/plink2-files/ALL/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="data/AA/variants/overlappingSNPs_chr{chr}.txt",
        IDs="data/AA/ids/{subdataset}/{gwas}/gwas.ids"
    output:
        "output/calculate_FGr/AA/{subdataset}/{gwas}/FGr_{contrasts}_{chr}.txt"
    resources:
        mem_mb=100000,
	time="03:00:00"
    threads: 16
    params:
        gp_prefix = "/scratch/jgblanc/ukbb/plink2-files/ALL/ukb_imp_chr{chr}_v3",
        out_prefix = "output/calculate_FGr/AA/{subdataset}/{gwas}/{contrasts}_{chr}"
    shell:
        """
        Rscript code/calculate_FGr/calc_FGr_chr.R {params.gp_prefix} {params.out_prefix} {input.r} {input.overlap_snps} {output} {input.IDs}
        """

# Add together individal chromosomes LOCO

rule concat_chr_FGr:
    input:
        expand("output/calculate_FGr/AA/{{subdataset}}/{{gwas}}/FGr_{{contrasts}}_{chr}.txt", chr = CHR)
    output:
        "output/calculate_FGr/AA/{subdataset}/{gwas}/FGr-LOCO_{contrasts}_{chr}.txt"
    params:
        chrNum = lambda wildcards: get_chr_num(wildcards.chr),
        prefix_in = "output/calculate_FGr/AA/{subdataset}/{gwas}/FGr_{contrasts}"
    shell:
        """
        Rscript code/calculate_FGr/concat_FGr_LOCO.R {params.prefix_in} {params.chrNum} {output}
        """

## GWAS panel PCA

rule make_tmp_plink2:
    input:
        gp="/scratch/jgblanc/ukbb/plink2-files/ALL/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="data/ukbb/ukbb_pc_snps.txt",
        IDs="data/AA/ids/{subdataset}/{gwas}/gwas.ids"
    output:
        "/scratch/jgblanc/ukbb/plink2-files/AA/{subdataset}/{gwas}/tmp_{chr}.pgen",
        "/scratch/jgblanc/ukbb/plink2-files/AA/{subdataset}/{gwas}/tmp_{chr}.pvar",
        "/scratch/jgblanc/ukbb/plink2-files/AA/{subdataset}/{gwas}/tmp_{chr}.psam"
    params:
        prefix_out = "/scratch/jgblanc/ukbb/plink2-files/AA/{subdataset}/{gwas}/tmp_{chr}",
        prefix_plink = "/scratch/jgblanc/ukbb/plink2-files/AA/ukb_imp_chr{chr}_v3"
    threads: 16
    resources:
        mem_mb=38000,
        time="00:30:00"
    shell:
        """
        plink2 --pfile {params.prefix_plink} \
        --keep {input.IDs} \
        --extract {input.overlap_snps} \
	--make-pgen \
        --out {params.prefix_out}
        """

rule merge_GWAS_PCA:
    input:
        gp_genos=expand("/scratch/jgblanc/ukbb/plink2-files/AA/{{subdataset}}/{{gwas}}/tmp_{chr}.pgen", chr=CHR)
    output:
        vec="data/AA/pca/{subdataset}/{gwas}/pca.eigenvec",
        val="data/AA/pca/{subdataset}/{gwas}/pca.eigenval"
    params:
        prefix_out = "data/AA/pca/{subdataset}/{gwas}/pca",
        merge_prefix = expand("/scratch/jgblanc/ukbb/plink2-files/ALL/{{subdataset}}/{{gwas}}/tmp_{chr}", chr=CHR, newline="\n"),
	remove_path = "/scratch/jgblanc/ukbb/plink2-files/ALL/{subdataset}/{gwas}/"
    threads: 16
    resources:
        mem_mb=50000,
        time="03:00:00"
    shell:
        """
	echo {params.merge_prefix} > {wildcards.subdataset}-tmp.txt
        tr ' ' '\n' < "{wildcards.subdataset}-tmp.txt" > "{wildcards.subdataset}-tmp_chrm_list.txt"
        plink2 --pmerge-list {wildcards.subdataset}-tmp_chrm_list.txt \
        --pca 40 approx \
	--memory 50000 \
	--threads 16 \
        --out {params.prefix_out}
        rm {wildcards.subdataset}-tmp*
	rm {params.remove_path}*
	rm {params.prefix_out}.p*
        """


## Run GWAS

rule assemble_fixed_covars:
    input:
        sex="data/phenotypes/genetic_sex_22001.txt",
        batch="data/phenotypes/genotype_measurement_batch_22000.txt"
    output:
        "output/run_gwas/fixed_covars.txt"
    shell:
        """
        Rscript code/run_gwas/assemble_fixed_covar.R {input.sex} {input.batch} {output}
        """

rule assemble_quant_covars_FGr:
    input:
        age="data/phenotypes/age_at_recruitment_21022.txt",
        covar="output/calculate_FGr/AA/{subdataset}/{gwas}/FGr-LOCO_{contrasts}_{chr}.txt",
        pcs="data/AA/pca/{subdataset}/{gwas}/pca.eigenvec"
    output:
        "output/run_gwas/AA/{subdataset}/{gwas}/covariates/FGr-LOCO_{contrasts}_{chr}-PC{pc}.txt"
    params:
        pcNum = lambda wildcards: get_chr_num(wildcards.pc)
    shell:
        """
        Rscript code/run_gwas/assemble_quant_covar.R {input.age} {input.covar} {input.pcs} {params.pcNum} {output}
        """

rule assemble_quant_covars:
    input:
        age="data/phenotypes/age_at_recruitment_21022.txt",
        pcs="data/AA/pca/{subdataset}/{gwas}/pca.eigenvec"
    output:
        "output/run_gwas/AA/{subdataset}/{gwas}/covariates/no-FGr_{contrasts}_{chr}-PC{pc}.txt"
    params:
        pcNum = lambda wildcards: get_chr_num(wildcards.pc)
    shell:
        """
        Rscript code/run_gwas/assemble_quant_covar_control.R {input.age} {input.pcs} {params.pcNum} {output}
        """

rule run_fastGWA:
    input:
        pheno="data/phenotypes/normalized/{phenotype}.txt",
        qcovar="output/run_gwas/AA/{subdataset}/{gwas}/covariates/{covar}_{contrasts}_{chr}-PC{pc}.txt",
        gp_genos="/scratch/jgblanc/ukbb/plink2-files/ALL/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="data/AA/variants/overlappingSNPs_chr{chr}.txt",
        covar="output/run_gwas/fixed_covars.txt",
        IDs="data/AA/ids/{subdataset}/{gwas}/gwas.ids"
    output:
        "/scratch/jgblanc/stratification-data_analysis/output/run_gwas/AA/{subdataset}/{gwas}/{phenotype}/raw_LR/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA",
    params:
        prefix_out = "/scratch/jgblanc/stratification-data_analysis/output/run_gwas/AA/{subdataset}/{gwas}/{phenotype}/raw_LR/{covar}_{contrasts}_{chr}-PC{pc}",
        prefix_plink = "/scratch/jgblanc/ukbb/plink2-files/ALL/ukb_imp_chr{chr}_v3"
    threads: 16
    resources:
        mem_mb=100,
        time="00:30:00"
    shell:
        """
        gcta  --pheno {input.pheno} \
        --keep {input.IDs} \
	--qcovar {input.qcovar} \
	--covar {input.covar} \
	--extract {input.overlap_snps} \
	--fastGWA-lr \
	--threads {threads} \
	--pfile {params.prefix_plink} \
	--out {params.prefix_out}
        """

## Pick minimum p-value per block

rule assign_snps_to_blocks:
    input:
        ldBlocks="data/LD_blocks/fourier_ls-all_parsed.bed",
        ss="/scratch/jgblanc/stratification-data_analysis/output/run_gwas/AA/{subdataset}/{gwas}/{phenotype}/raw_LR/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA",
        r="data/AA/r/{subdataset}/{gwas}/{contrasts}_chr{chr}.rvec",
    output:
        betas="/scratch/jgblanc/stratification-data_analysis/output/pga_test/AA/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}_{chr}-PC{pc}.betas"
    params:
        pt = PVALUE_THRESHOLD
    shell:
        """
        Rscript code/pga_test/assign_snps_to_blocks_LR.R {input.ldBlocks} {input.ss} {input.r} {output.betas} {params.pt}
        rm {input.ss}
        """

## Run selection test

rule concat_snps:
    input:
        expand("/scratch/jgblanc/stratification-data_analysis/output/pga_test/AA/{{subdataset}}/{{gwas}}/{{phenotype}}/formated_ss_LR/{{covar}}_{{contrasts}}_{chr}-PC{{pc}}.betas", chr=CHR)
    output:
        "output/pga_test/AA/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}.all-PC{pc}.betas"
    shell:
        """
        cat {input} > {output}
        rm {input}
        """

rule pga_test:
    input:
        "output/pga_test/AA/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}.all-PC{pc}.betas"
    output:
        results="output/pga_test/AA/{subdataset}/{gwas}/{phenotype}/results_LR/{covar}_{contrasts}.all-PC{pc}.results",
	gzip="output/pga_test/AA/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}.all-PC{pc}.betas.gz"
    shell:
        """
        Rscript code/pga_test/run_test_jacknife_LR.R {input} {output.results}
	gzip {input}
        """

## Compile Results

rule concat_pga_test:
    input:
        expand("output/pga_test/AA/{subdataset}/{gwas}/{phenotype}/results_LR/{covar}_{contrasts}.all-PC{pc}.results", covar = COVAR, contrasts = CONTRASTS, pc = PC, phenotype = PHENOTYPE, gwas=GWAS, subdataset=SUBDATASET)
    output:
        "plots/AA/results.txt"
    shell:
        """
        Rscript code/pga_test/concat_results_Sims.R {output} {input}
        """

## Compute FGr vs PCs

rule compute_PC_FGr:
    input:
        Fgr=expand("output/calculate_FGr/AA/{{subdataset}}/FGr_{{contrasts}}_{chr}.txt", chr = CHR),
        PCs = "data/AA/{gwas}/pca.eigenvec"
    output:
        "plots/AA/{subdataset}/{gwas}/{contrasts}_PCs_FGr.txt"
    params:
        prefix_in = "output/calculate_FGr/AA/{subdataset}/FGr_{contrasts}"
    shell:
        """
        Rscript code/calculate_FGr/compute_PC_FGR_model.R {params.prefix_in} {input.PCs} {output}
        """

