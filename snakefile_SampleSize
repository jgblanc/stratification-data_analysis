# Snakefile to run full data analysis with any set of constrasts
CHR =[]
for i in range(1, 23):
  CHR.append(str(i))
DATASET = ["SS"]
SUBDATASET = ["t100"]
#SUBDATASET = ["t100000", "t1000", "t100", "t50000", "t5000"]
CONTRASTS = ["east"]
PC = [0, 10, 40]
#GWAS=["wbs50000", "wbs100000", "wbs300000"]
GWAS = ["g50000"]
COVAR = ["no-FGr", "FGr-LOCO"]
PHENOTYPE=[ "Educational_Attainment_6138"]
#PHENOTYPE = [f.split(".")[0] for f in os.listdir("data/phenotypes/normalized/") if f.endswith('.txt')]
PVALUE_THRESHOLD = "1e-5"
#print(CONTRASTS)


wildcard_constraints:
    subdataset="t[0-9]*",
    gwas="g[0-9]*"


def get_chr_num(x):
  out = int(x)
  return out

rule all:
    input:
        expand("output/pga_test/SS/{subdataset}/{gwas}/{phenotype}/results_LR/{covar}_{contrasts}.all-PC{pc}.results", chr=CHR, subdataset=SUBDATASET, gwas=GWAS, phenotype=PHENOTYPE, covar=COVAR, contrasts=CONTRASTS, pc=PC)


## Get final set of SNP (White MAF > 1%)

rule get_overlapping_snps:
    input:
        "data/ukbb/variants/White/ukb_imp_chr{chr}_v3.afreq"
    output:
        "data/SS/variants/overlappingSNPs_chr{chr}.txt"
    shell:
        """
        cut -f2,3 {input} > {output}
        """

# Make Birth Coordinates Test Vector

rule get_TestVector:
    input:
        North="data/phenotypes/PlaceOfBirthNorthCord_129.txt",
        East = "data/phenotypes/PlaceOfBirthEastCord_130.txt",
        ids="data/ukbb/SS/tp/{subdataset}.txt"
    output:
        north = "data/SS/{subdataset}/TestVecs/north.txt",
        east = "data/SS/{subdataset}/TestVecs/east.txt"
    shell:
        """
        Rscript code/SS/get_Tvec.R {input.North} {input.East} {input.ids} {output.north} {output.east}
        """

rule compute_r:
    input:
        TvecNorth="data/SS/{subdataset}/TestVecs/north.txt",
        TvecEast="data/SS/{subdataset}/TestVecs/east.txt",
        IDs="data/ukbb/SS/tp/{subdataset}.txt",
        snps="data/SS/variants/overlappingSNPs_chr{chr}.txt"
    output:
        North="data/SS/{subdataset}/r/north_chr{chr}.rvec",
        East="data/SS/{subdataset}/r/east_chr{chr}.rvec"
    params:
        prefix_tp = "/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3",
        prefix_out = "data/SS/{subdataset}/r/"
    shell:
        """
        Rscript code/SS/compute_cord_r.R {params.prefix_tp} {input.TvecNorth} {input.TvecEast} {params.prefix_out} {input.snps} {output.North} {output.East} {input.IDs}
        rm {params.prefix_out}xt_temp.*
        """

## Compute FGr

rule compute_FGr_chr:
    input:
        r="data/SS/{subdataset}/r/{contrasts}_chr{chr}.rvec",
        gp_genos="/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="data/SS/variants/overlappingSNPs_chr{chr}.txt",
        IDs="data/ukbb/SS/gp/{gwas}.txt"
    output:
        "output/calculate_FGr/SS/{subdataset}/{gwas}/FGr_{contrasts}_{chr}.txt"
    resources:
        mem_mb=100000,
	time="03:00:00"
    threads: 16
    params:
        gp_prefix = "/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3",
        out_prefix = "output/calculate_FGr/SS/{subdataset}/{gwas}/{contrasts}_{chr}"
    shell:
        """
        Rscript code/calculate_FGr/calc_FGr_chr.R {params.gp_prefix} {params.out_prefix} {input.r} {input.overlap_snps} {output} {input.IDs}
        """

# Add together individal chromosomes LOCO

rule concat_chr_FGr:
    input:
        expand("output/calculate_FGr/SS/{{subdataset}}/{{gwas}}/FGr_{{contrasts}}_{chr}.txt", chr = CHR)
    output:
        "output/calculate_FGr/SS/{subdataset}/{gwas}/FGr-LOCO_{contrasts}_{chr}.txt"
    params:
        chrNum = lambda wildcards: get_chr_num(wildcards.chr),
        prefix_in = "output/calculate_FGr/SS/{subdataset}/{gwas}/FGr_{contrasts}"
    shell:
        """
        Rscript code/calculate_FGr/concat_FGr_LOCO.R {params.prefix_in} {params.chrNum} {output}
        """


## GWAS panel PCA

rule make_tmp_plink2:
    input:
        gp="/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="data/ukbb/ukbb_pc_snps.txt",
        IDs="data/ukbb/SS/gp/{gwas}.txt"
    output:
        "/scratch/jgblanc/ukbb/plink2-files/White/{gwas}/tmp_{chr}.pgen",
        "/scratch/jgblanc/ukbb/plink2-files/White/{gwas}/tmp_{chr}.pvar",
        "/scratch/jgblanc/ukbb/plink2-files/White/{gwas}/tmp_{chr}.psam"
    params:
        prefix_out = "/scratch/jgblanc/ukbb/plink2-files/White/{gwas}/tmp_{chr}",
        prefix_plink = "/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3"
    threads: 16
    resources:
        mem_mb=38000,
        time="00:30:00"
    shell:
        """
        plink2 --pfile {params.prefix_plink} \
        --keep {input.IDs} \
        --extract {input.overlap_snps} \
	--make-pgen \
        --out {params.prefix_out}
        """


rule merge_GWAS_PCA:
    input:
        gp_genos=expand("/scratch/jgblanc/ukbb/plink2-files/White/{{gwas}}/tmp_{chr}.pgen", chr=CHR)
    output:
        vec="data/SS/{gwas}/pca.eigenvec",
        val="data/SS/{gwas}/pca.eigenval"
    params:
        prefix_out = "data/SS/{gwas}/pca",
        merge_prefix = expand("/scratch/jgblanc/ukbb/plink2-files/White/{{gwas}}/tmp_{chr}", chr=CHR, newline="\n"),
	remove_path = "/scratch/jgblanc/ukbb/plink2-files/White/{gwas}/"
    threads: 16
    resources:
        mem_mb=38000,
        time="03:00:00"
    shell:
        """
	echo {params.merge_prefix} > tmp.txt
        tr ' ' '\n' < "tmp.txt" > "tmp_chrm_list.txt"
        plink2 --pmerge-list  tmp_chrm_list.txt \
        --pca 40 approx \
        --out {params.prefix_out}
        rm tmp*
	rm {params.remove_path}*
	rm {params.prefix_out}.p*
        """


## Run GWAS

rule assemble_fixed_covars:
    input:
        sex="data/phenotypes/genetic_sex_22001.txt",
        batch="data/phenotypes/genotype_measurement_batch_22000.txt"
    output:
        "output/run_gwas/fixed_covars.txt"
    shell:
        """
        Rscript code/run_gwas/assemble_fixed_covar.R {input.sex} {input.batch} {output}
        """

rule assemble_quant_covars_FGr:
    input:
        age="data/phenotypes/age_at_recruitment_21022.txt",
        covar="output/calculate_FGr/SS/{subdataset}/{gwas}/FGr-LOCO_{contrasts}_{chr}.txt",
        pcs="data/SS/{gwas}/pca.eigenvec"
    output:
        "output/run_gwas/SS/{subdataset}/{gwas}/covariates/FGr-LOCO_{contrasts}_{chr}-PC{pc}.txt"
    params:
        pcNum = lambda wildcards: get_chr_num(wildcards.pc)
    shell:
        """
        Rscript code/run_gwas/assemble_quant_covar.R {input.age} {input.covar} {input.pcs} {params.pcNum} {output}
        """

rule assemble_quant_covars:
    input:
        age="data/phenotypes/age_at_recruitment_21022.txt",
        pcs="data/SS/{gwas}/pca.eigenvec"
    output:
        "output/run_gwas/SS/{subdataset}/{gwas}/covariates/no-FGr_{contrasts}_{chr}-PC{pc}.txt"
    params:
        pcNum = lambda wildcards: get_chr_num(wildcards.pc)
    shell:
        """
        Rscript code/run_gwas/assemble_quant_covar_control.R {input.age} {input.pcs} {params.pcNum} {output}
        """

rule run_fastGWA:
    input:
        pheno="data/phenotypes/{phenotype}.txt",
        qcovar="output/run_gwas/SS/{subdataset}/{gwas}/covariates/{covar}_{contrasts}_{chr}-PC{pc}.txt",
        gp_genos="/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="data/SS/variants/overlappingSNPs_chr{chr}.txt",
        covar="output/run_gwas/fixed_covars.txt",
        IDs="data/ukbb/SS/gp/{gwas}.txt"
    output:
        "/scratch/jgblanc/stratification-data_analysis/output/run_gwas/SS/{subdataset}/{gwas}/{phenotype}/raw_LR/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA",
    params:
        prefix_out = "/scratch/jgblanc/stratification-data_analysis/output/run_gwas/SS/{subdataset}/{gwas}/{phenotype}/raw_LR/{covar}_{contrasts}_{chr}-PC{pc}",
        prefix_plink = "/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3"
    threads: 16
    resources:
        mem_mb=100,
        time="00:30:00"
    shell:
        """
        gcta  --pheno {input.pheno} \
        --keep {input.IDs} \
	--qcovar {input.qcovar} \
	--covar {input.covar} \
	--extract {input.overlap_snps} \
	--fastGWA-lr \
	--threads {threads} \
	--pfile {params.prefix_plink} \
	--out {params.prefix_out}
        """

## Pick minimum p-value per block

rule assign_snps_to_blocks:
    input:
        ldBlocks="data/LD_blocks/fourier_ls-all_parsed.bed",
        ss="/scratch/jgblanc/stratification-data_analysis/output/run_gwas/SS/{subdataset}/{gwas}/{phenotype}/raw_LR/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA",
        r="data/SS/{subdataset}/r/{contrasts}_chr{chr}.rvec",
    output:
        betas="/scratch/jgblanc/stratification-data_analysis/output/pga_test/SS/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}_{chr}-PC{pc}.betas"
    params:
        pt = PVALUE_THRESHOLD
    shell:
        """
        Rscript code/pga_test/assign_snps_to_blocks_LR.R {input.ldBlocks} {input.ss} {input.r} {output.betas} {params.pt}
        rm {input.ss}
        """

## Run selection test

rule concat_snps:
    input:
        expand("/scratch/jgblanc/stratification-data_analysis/output/pga_test/SS/{{subdataset}}/{{gwas}}/{{phenotype}}/formated_ss_LR/{{covar}}_{{contrasts}}_{chr}-PC{{pc}}.betas", chr=CHR)
    output:
        "output/pga_test/SS/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}.all-PC{pc}.betas"
    shell:
        """
        cat {input} > {output}
        rm {input}
        """

rule pga_test:
    input:
        "output/pga_test/SS/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}.all-PC{pc}.betas"
    output:
        results="output/pga_test/SS/{subdataset}/{gwas}/{phenotype}/results_LR/{covar}_{contrasts}.all-PC{pc}.results",
	gzip="output/pga_test/SS/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}.all-PC{pc}.betas.gz"
    shell:
        """
        Rscript code/pga_test/run_test_jacknife_LR.R {input} {output.results}
	gzip {input}
        """

## Compile Results

rule concat_pga_test:
    input:
        expand("output/pga_test/SS/{{subdataset}}/{{gwas}}/{phenotype}/results_LR/{covar}_{contrasts}.all-PC{pc}.results", covar = COVAR, contrasts = CONTRASTS, pc = PC, phenotype = PHENOTYPE)
    output:
        "plots/SS/{subdataset}/{gwas}/results_PCs.txt"
    shell:
        """
        Rscript code/pga_test/concat_results_LR.R {output} {input}
        """

## Compute FGr vs PCs

rule compute_PC_FGr:
    input:
        Fgr=expand("output/calculate_FGr/SS/{{subdataset}}/FGr_{{contrasts}}_{chr}.txt", chr = CHR),
        PCs = "data/SS/{gwas}/pca.eigenvec"
    output:
        "plots/SS/{subdataset}/{gwas}/{contrasts}_PCs_FGr.txt"
    params:
        prefix_in = "output/calculate_FGr/SS/{subdataset}/FGr_{contrasts}"
    shell:
        """
        Rscript code/calculate_FGr/compute_PC_FGR_model.R {params.prefix_in} {input.PCs} {output}
        """

## Test panel PCA

rule make_tmp_plink2_test:
    input:
        gp="/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="data/ukbb/ukbb_pc_snps.txt",
        IDs="data/ukbb/SS/tp/{subdataset}.txt"
    output:
        "/scratch/jgblanc/ukbb/plink2-files/White/{subdataset}/tmp_{chr}.pgen",
        "/scratch/jgblanc/ukbb/plink2-files/White/{subdataset}/tmp_{chr}.pvar",
        "/scratch/jgblanc/ukbb/plink2-files/White/{subdataset}/tmp_{chr}.psam"
    params:
        prefix_out = "/scratch/jgblanc/ukbb/plink2-files/White/{subdataset}/tmp_{chr}",
        prefix_plink = "/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3"
    threads: 16
    resources:
        mem_mb=38000,
        time="00:30:00"
    shell:
        """
        plink2 --pfile {params.prefix_plink} \
        --keep {input.IDs} \
        --extract {input.overlap_snps} \
	--make-pgen \
        --out {params.prefix_out}
        """


rule merge_Test_PCA:
    input:
        gp_genos=expand("/scratch/jgblanc/ukbb/plink2-files/White/{{subdataset}}/tmp_{chr}.pgen", chr=CHR)
    output:
        vec="data/SS/{subdataset}/pca.eigenvec",
        val="data/SS/{subdataset}/pca.eigenval"
    params:
        prefix_out = "data/SS/{subdataset}/pca",
        merge_prefix = expand("/scratch/jgblanc/ukbb/plink2-files/White/{{subdataset}}/tmp_{chr}", chr=CHR, newline="\n"),
	remove_path = "/scratch/jgblanc/ukbb/plink2-files/White/{subdataset}/"
    threads: 16
    resources:
        mem_mb=38000,
        time="03:00:00"
    shell:
        """
	echo {params.merge_prefix} > tmp.txt
        tr ' ' '\n' < "tmp.txt" > "tmp_chrm_list.txt"
        plink2 --pmerge-list  tmp_chrm_list.txt \
        --pca 40 approx \
        --out {params.prefix_out}
        rm tmp*
	rm {params.remove_path}*
	rm {params.prefix_out}.p*
        """


## Make PCs Test Vector

rule get_TestVector_PCA:
    input:
        pca="data/SS/{subdataset}/pca.eigenvec"
    output:
        pc1 = "data/SS/{subdataset}/TestVecs/pc1.txt",
        pc2 = "data/SS/{subdataset}/TestVecs/pc2.txt",
        pc3 = "data/SS/{subdataset}/TestVecs/pc3.txt",
        pc10 = "data/SS/{subdataset}/TestVecs/pc10.txt",
        pc20 = "data/SS/{subdataset}/TestVecs/pc20.txt"
    params:
        prefix_out = "data/SS/{subdataset}/TestVecs/"
    shell:
        """
        Rscript code/SS/get_Tvec_PCs.R {input.pca} {params.prefix_out} 1 2 3 10 20
        """

## Compute r

rule compute_r_PCA:
    input:
        pc1 = "data/SS/{subdataset}/TestVecs/pc1.txt",
        pc2 = "data/SS/{subdataset}/TestVecs/pc2.txt",
        pc3 = "data/SS/{subdataset}/TestVecs/pc3.txt",
        pc4 = "data/SS/{subdataset}/TestVecs/pc10.txt",
        pc5 = "data/SS/{subdataset}/TestVecs/pc20.txt",
        IDs="data/ukbb/SS/tp/{subdataset}.txt",
        snps="data/SS/variants/overlappingSNPs_chr{chr}.txt"
    output:
        pc1="data/SS/{subdataset}/r/pc1_chr{chr}.rvec",
        pc2="data/SS/{subdataset}/r/pc2_chr{chr}.rvec",
        pc3="data/SS/{subdataset}/r/pc3_chr{chr}.rvec",
        pc4="data/SS/{subdataset}/r/pc10_chr{chr}.rvec",
        pc5="data/SS/{subdataset}/r/pc20_chr{chr}.rvec",
    params:
        prefix_tp = "/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3",
        prefix_out = "data/SS/{subdataset}/r/{chr}"
    shell:
        """
        Rscript code/SS/compute_pca_r.R {params.prefix_tp} {params.prefix_out} {input.snps} {input.IDs} {input.pc1} {input.pc2} {input.pc3} {input.pc4} {input.pc5}  {output.pc1} {output.pc2} {output.pc3} {output.pc4} {output.pc5}
        rm {params.prefix_out}xt_temp.*
        """
