# Snakefile to run full data analysis with any set of constrasts
CHR =["22"]
#for i in range(1, 23):
#  CHR.append(str(i))
DATASET = ["SS"]
SUBDATASET = ["t100000", "t1000", "t100", "t50000", "t5000"]
CONTRASTS = ["north", "east"]
PC = [0,2,10]
GWAS=["ALL"]
COVAR = ["no-FGr", "FGr-LOCO"]
PHENOTYPE=[ "HipCircumference_Normalized_49"]
#PHENOTYPE = [f.split(".")[0] for f in os.listdir("data/phenotypes/normalized/") if f.endswith('.txt')]
PVALUE_THRESHOLD = "1e-5"
#print(CONTRASTS)

def get_chr_num(x):
  out = int(x)
  return out

rule all:
    input:
        expand("data/SS/{subdataset}/r/North_chr{chr}.rvec", chr=CHR, subdataset=SUBDATASET, contrasts=CONTRASTS)


# Get final set of SNP (White MAF > 1%)

rule get_overlapping_snps:
    input:
        "data/ukbb/variants/White/ukb_imp_chr{chr}_v3.afreq"
    output:
        "data/SS/variants/overlappingSNPs_chr{chr}.txt"
    shell:
        """
        cut -f2,3 {input} > {output}
        """


# Make Birth Coordinates Test Vector

rule get_TestVector:
    input:
        North="data/phenotypes/PlaceOfBirthNorthCord_129.txt",
        East = "data/phenotypes/PlaceOfBirthEastCord_130.txt",
        ids="data/ukbb/SS/tp/{subdataset}.txt"
    output:
        north = "data/SS/{subdataset}/TestVecs/north.txt",
        east = "data/SS/{subdataset}/TestVecs/east.txt"
    shell:
        """
        Rscript code/SS/get_Tvec.R {input.North} {input.East} {input.ids} {output.north} {output.east}
        """

rule compute_r:
    input:
        TvecNorth="data/SS/{subdataset}/TestVecs/North.txt",
        TvecEast="data/SS/{subdataset}/TestVecs/East.txt",
        IDs="data/ukbb/SS/tp/{subdataset}.txt",
        snps="data/SS/variants/overlappingSNPs_chr{chr}.txt"
    output:
        North="data/SS/{subdataset}/r/North_chr{chr}.rvec",
        East="data/SS/{subdataset}/r/East_chr{chr}.rvec"
    params:
        prefix_tp = "/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3",
        prefix_out = "data/SS/{subdataset}/r/"
    shell:
        """
        Rscript code/SS/compute_cord_r.R {params.prefix_tp} {input.TvecNorth} {input.TvecEast} {params.prefix_out} {input.snps} {output.North} {output.East} {input.IDs}
        rm {params.prefix_out}xt_temp.*
        """



## Compute FGr

rule compute_FGr_chr:
    input:
        r="data/{dataset}/{subdataset}/r/{contrasts}_chr{chr}.rvec",
        gp_genos="/scratch/jgblanc/ukbb/plink2-files/White/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="data/{dataset}/{subdataset}/{gwas}/variants/overlappingSNPs_chr{chr}.txt"
    output:
        "output/calculate_FGr/{dataset}/{subdataset}/{gwas}/FGr_{contrasts}_{chr}.txt"
    resources:
        mem_mb=2000,
	time="03:00:00"
    params:
        gp_prefix = "/scratch/jgblanc/ukbb/plink2-files/{gwas}/ukb_imp_chr{chr}_v3",
        out_prefix = "output/calculate_FGr/{dataset}/{subdataset}/{gwas}/{contrasts}_{chr}"
    shell:
        """
        Rscript code/calculate_FGr/calc_FGr_chr.R {params.gp_prefix} {params.out_prefix} {input.r} {input.overlap_snps} {output}
        """

# Add together individal chromosomes LOCO

rule concat_chr_FGr:
    input:
        expand("output/calculate_FGr/{{dataset}}/{{subdataset}}/{{gwas}}/FGr_{{contrasts}}_{chr}.txt", chr = CHR)
    output:
        "output/calculate_FGr/{dataset}/{subdataset}/{gwas}/FGr-LOCO_{contrasts}_{chr}.txt"
    params:
        chrNum = lambda wildcards: get_chr_num(wildcards.chr),
        prefix_in = "output/calculate_FGr/{dataset}/{subdataset}/{gwas}/FGr_{contrasts}"
    shell:
        """
        Rscript code/calculate_FGr/concat_FGr_LOCO.R {params.prefix_in} {params.chrNum} {output}
        """

## Run GWAS

rule assemble_fixed_covars:
    input:
        sex="data/phenotypes/genetic_sex_22001.txt",
        batch="data/phenotypes/genotype_measurement_batch_22000.txt"
    output:
        "output/run_gwas/fixed_covars.txt"
    shell:
        """
        Rscript code/run_gwas/assemble_fixed_covar.R {input.sex} {input.batch} {output}
        """

rule assemble_quant_covars_FGr:
    input:
        age="data/phenotypes/age_at_recruitment_21022.txt",
        covar="output/calculate_FGr/{dataset}/{subdataset}/{gwas}/FGr-LOCO_{contrasts}_{chr}.txt",
        pcs="data/phenotypes/genetic_PC_22009.txt"
    output:
        "output/run_gwas/{dataset}/{subdataset}/{gwas}/covariates/FGr-LOCO_{contrasts}_{chr}-PC{pc}.txt"
    params:
        pcNum = lambda wildcards: get_chr_num(wildcards.pc)
    shell:
        """
        Rscript code/run_gwas/assemble_quant_covar.R {input.age} {input.covar} {input.pcs} {params.pcNum} {output}
        """

rule assemble_quant_covars:
    input:
        age="data/phenotypes/age_at_recruitment_21022.txt",
        pcs="data/phenotypes/genetic_PC_22009.txt"
    output:
        "output/run_gwas/{dataset}/{subdataset}/{gwas}/covariates/no-FGr_{contrasts}_{chr}-PC{pc}.txt"
    params:
        pcNum = lambda wildcards: get_chr_num(wildcards.pc)
    shell:
        """
        Rscript code/run_gwas/assemble_quant_covar_control.R {input.age} {input.pcs} {params.pcNum} {output}
        """

rule run_fastGWA:
    input:
        pheno="data/phenotypes/normalized/{phenotype}.txt",
        qcovar="output/run_gwas/{dataset}/{subdataset}/{gwas}/covariates/{covar}_{contrasts}_{chr}-PC{pc}.txt",
        gp_genos="/scratch/jgblanc/ukbb/plink2-files/{gwas}/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="data/{dataset}/{subdataset}/{gwas}/variants/overlappingSNPs_chr{chr}.txt",
        covar="output/run_gwas/fixed_covars.txt"
    output:
        "/scratch/jgblanc/stratification-data_analysis/output/run_gwas/{dataset}/{subdataset}/{gwas}/{phenotype}/raw_LR/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA",
    params:
        prefix_out = "/scratch/jgblanc/stratification-data_analysis/output/run_gwas/{dataset}/{subdataset}/{gwas}/{phenotype}/raw_LR/{covar}_{contrasts}_{chr}-PC{pc}",
        prefix_plink = "/scratch/jgblanc/ukbb/plink2-files/{gwas}/ukb_imp_chr{chr}_v3"
    threads: 16
    resources:
        mem_mb=100,
        time="00:30:00"
    shell:
        """
        gcta  --pheno {input.pheno} \
	--qcovar {input.qcovar} \
	--covar {input.covar} \
	--extract {input.overlap_snps} \
	--fastGWA-lr \
	--threads {threads} \
	--pfile {params.prefix_plink} \
	--out {params.prefix_out}
        """

## Pick minimum p-value per block

rule assign_snps_to_blocks:
    input:
        ldBlocks="data/LD_blocks/fourier_ls-all_parsed.bed",
        ss="/scratch/jgblanc/stratification-data_analysis/output/run_gwas/{dataset}/{subdataset}/{gwas}/{phenotype}/raw_LR/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA",
        r="data/{dataset}/{subdataset}/r/{contrasts}_chr{chr}.rvec",
    output:
        betas="/scratch/jgblanc/stratification-data_analysis/output/pga_test/{dataset}/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}_{chr}-PC{pc}.betas"
    params:
        pt = PVALUE_THRESHOLD
    shell:
        """
        Rscript code/pga_test/assign_snps_to_blocks_LR.R {input.ldBlocks} {input.ss} {input.r} {output.betas} {params.pt}
        rm {input.ss}
        """

## Run selection test

rule concat_snps:
    input:
        expand("/scratch/jgblanc/stratification-data_analysis/output/pga_test/{{dataset}}/{{subdataset}}/{{gwas}}/{{phenotype}}/formated_ss_LR/{{covar}}_{{contrasts}}_{chr}-PC{{pc}}.betas", chr=CHR)
    output:
        "output/pga_test/{dataset}/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}.all-PC{pc}.betas"
    shell:
        """
        cat {input} > {output}
        rm {input}
        """

rule pga_test:
    input:
        "output/pga_test/{dataset}/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}.all-PC{pc}.betas"
    output:
        results="output/pga_test/{dataset}/{subdataset}/{gwas}/{phenotype}/results_LR/{covar}_{contrasts}.all-PC{pc}.results",
	gzip="output/pga_test/{dataset}/{subdataset}/{gwas}/{phenotype}/formated_ss_LR/{covar}_{contrasts}.all-PC{pc}.betas.gz"
    shell:
        """
        Rscript code/pga_test/run_test_jacknife_LR.R {input} {output.results}
	gzip {input}
        """

## Compile Results

rule concat_pga_test:
    input:
        expand("output/pga_test/{{dataset}}/{{subdataset}}/{{gwas}}/{phenotype}/results_LR/{covar}_{contrasts}.all-PC{pc}.results", covar = COVAR, contrasts = CONTRASTS, pc = PC, phenotype = PHENOTYPE)
    output:
        "plots/{dataset}/{subdataset}/{gwas}/results_LR_LatLong.txt"
    shell:
        """
        Rscript code/pga_test/concat_results_LR.R {output} {input}
        """

## Compute R2 in non-WBS Europeans

rule format_ss_pgs:
    input:
        "output/pga_test/{dataset}/{subdataset}/{gwas}/{phenotype}/formated_ss/{covar}_{contrasts}.all-PC{pc}.betas.gz"
    output:
        "output/prs/{dataset}/{subdataset}/{gwas}/{phenotype}/plink_format/{covar}_{contrasts}.all-PC{pc}.betas"
    shell:
        """
        Rscript code/prs/format_ss_for_plink.R {input} {output}
        """

rule pgs_sscore:
    input:
        betas="output/prs/{dataset}/{subdataset}/{gwas}/{phenotype}/plink_format/{covar}_{contrasts}.all-PC{pc}.betas",
        gp_genos="/scratch/jgblanc/ukbb/plink2-files/ALL/ukb_imp_chr{chr}_v3.pgen",
        IDs="data/ukbb/nonWBS-White.ids"
    output:
        "output/prs/{dataset}/{subdataset}/{gwas}/{phenotype}/pgs_europeans/{covar}_{contrasts}.{chr}-PC{pc}.sscore"
    params:
        prefix_out="output/prs/{dataset}/{subdataset}/{gwas}/{phenotype}/pgs_europeans/{covar}_{contrasts}.{chr}-PC{pc}",
        prefix_in="/scratch/jgblanc/ukbb/plink2-files/ALL/ukb_imp_chr{chr}_v3"
    threads: 16
    shell:
        """
        plink2 --pfile {params.prefix_in} \
        --keep {input.IDs} \
        --score {input.betas} center cols=dosagesum,scoresums  \
        --out {params.prefix_out}
        """

rule combine_pgs:
    input:
        expand("output/prs/{{dataset}}/{{subdataset}}/{{gwas}}/{{phenotype}}/pgs_europeans/{{covar}}_{{contrasts}}.{chr}-PC{{pc}}.sscore", chr=CHR)
    output:
        "output/prs/{dataset}/{subdataset}/{gwas}/{phenotype}/pgs_europeans/{covar}_{contrasts}.all-PC{pc}.sscore"
    params:
        all_chrs = CHR,
        num_chr = len(CHR),
        prefix_one = "output/prs/{dataset}/{subdataset}/{gwas}/{phenotype}/pgs_europeans/{covar}_{contrasts}",
        prefix_two = "-PC{pc}.sscore"
    shell:
        """
        Rscript code/prs/combine_chr.R {params.num_chr} {params.prefix_one} {params.prefix_two} {output} {params.all_chrs}
	      rm {input}
	      rm {params.prefix_one}*.log
        """

rule calculate_r2:
    input:
        phenotype = "data/phenotypes/normalized/{phenotype}.txt",
        pgs = "output/prs/{dataset}/{subdataset}/{gwas}/{phenotype}/pgs_europeans/{covar}_{contrasts}.all-PC{pc}.sscore"
    output:
        "output/prs/{dataset}/{subdataset}/{gwas}/{phenotype}/pgs_europeans/{covar}_{contrasts}.all-PC{pc}.r2"
    shell:
        """
        Rscript code/prs/compute_r2.R {input.phenotype} {input.pgs} {output}
        """

rule concat_r2:
    input:
        expand("output/prs/{{dataset}}/{{subdataset}}/{{gwas}}/{phenotype}/pgs_europeans/{covar}_{contrasts}.all-PC{pc}.r2", covar = COVAR, contrasts = CONTRASTS, pc = PC, phenotype = PHENOTYPE)
    output:
        "plots/{dataset}/{subdataset}/{gwas}/r2_results.txt"
    shell:
        """
        Rscript code/prs/concat_r2.R {output} {input}
        """


# Compute FGr vs PCs

rule compute_PC_FGr:
    input:
        Fgr=expand("output/calculate_FGr/{{dataset}}/{{subdataset}}/{{gwas}}/FGr_{{contrasts}}_{chr}.txt", chr = CHR),
        PCs = "data/phenotypes/genetic_PC_22009.txt"
    output:
        "output/calculate_FGr/{dataset}/{subdataset}/{gwas}/{contrasts}_PCs_FGr.txt"
    params:
        prefix_in = "output/calculate_FGr/{dataset}/{subdataset}/{gwas}/FGr_{contrasts}"
    shell:
        """
        Rscript code/calculate_FGr/compute_PC_FGR_model.R {params.prefix_in} {input.PCs} {output}
        """
