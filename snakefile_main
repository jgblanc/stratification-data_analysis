# Snakefile to run full data analysis with any set of constrasts
CHR =[]
for i in range(1, 23):
  CHR.append(str(i))
ROOT = ["/gpfs/data/berg-lab/jgblanc/stratification-data_analysis"]
DATASET = ["hgdp"]
SUBDATASET = ["EUR"]
TMP = [f.split(".")[0] for f in os.listdir("data/hgdp/r/EUR/") if f.endswith('_chr9.rvec')]
#CONTRASTS = [s.rstrip("_chr9") for s in TMP]
CONTRASTS = ["French-Sardinian"]
PC = [40]
GWAS=["WBS"]
COVAR = ["control","FGr-LOCO"]

def get_chr_num(x):
  out = int(x)
  return out

rule all:
    input:
        expand("{root}/output/prs/{dataset}/{subdataset}/vilma/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}.estimates.tsv", root=ROOT, chr=CHR, dataset = DATASET, subdataset=SUBDATASET, contrasts=CONTRASTS, pc=PC, gwas=GWAS, covar=COVAR)


## Compute FGr

rule compute_FGr_chr:
    input:
        r="{root}/data/{dataset}/r/{subdataset}/{contrasts}_chr{chr}.rvec",
        gp_genos="/scratch/jgblanc/ukbb/plink2-files/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="{root}/data/{dataset}/variants/{subdataset}/overlappingSNPs_chr{chr}.txt"
    output:
        "{root}/output/calculate_FGr/{dataset}/{subdataset}/FGr_{contrasts}_{chr}.txt"
    resources:
        mem_mb=2000,
	walltime="00:03:00:00"
    params:
        gp_prefix = "/scratch/jgblanc/ukbb/plink2-files/ukb_imp_chr{chr}_v3",
        out_prefix = "{root}/output/calculate_FGr/{dataset}/{subdataset}/{contrasts}_{chr}"
    shell:
        """
        Rscript code/calculate_FGr/calc_FGr_chr.R {params.gp_prefix} {params.out_prefix} {input.r} {input.overlap_snps} {output}
        """

# Add together individal chromosomes LOCO

rule concat_chr_Tm:
    input:
        expand("{{root}}/output/calculate_FGr/{{dataset}}/{{subdataset}}/FGr_{{contrasts}}_{chr}.txt", chr = CHR)
    output:
        "{root}/output/calculate_FGr/{dataset}/{subdataset}/FGr-LOCO_{contrasts}_{chr}.txt"
    params:
        chrNum = lambda wildcards: get_chr_num(wildcards.chr),
        prefix_in = "{root}/output/calculate_FGr/{dataset}/{subdataset}/FGr_{contrasts}"
    shell:
        """
        Rscript code/calculate_FGr/concat_FGr_LOCO.R {params.prefix_in} {params.chrNum} {output}
        """

## Run GWAS

rule assemble_fixed_covars:
    input:
        sex="{root}/data/phenotypes/genetic_sex_22001.txt",
        batch="{root}/data/phenotypes/genotype_measurement_batch_22000.txt"
    output:
        "{root}/output/run_gwas/covars/fixed.txt"
    shell:
        """
        Rscript code/run_gwas/assemble_fixed_covar.R {input.sex} {input.batch} {output}
        """

rule assemble_quant_covars:
    input:
        age="{root}/data/phenotypes/age_at_recruitment_21022.txt",
        covar="{root}/output/calculate_FGr/{dataset}/{subdataset}/FGr-LOCO_{contrasts}_{chr}.txt",
        pcs="{root}/data/phenotypes/genetic_PC_22009.txt"
    output:
        "{root}/output/run_gwas/covars/{dataset}/{subdataset}/FGr-LOCO_{contrasts}_{chr}-PC{pc}.txt"
    params:
        pcNum = lambda wildcards: get_chr_num(wildcards.pc)
    shell:
        """
        Rscript code/run_gwas/assemble_quant_covar.R {input.age} {input.covar} {input.pcs} {params.pcNum} {output}
        """

rule assemble_quant_covars_control:
    input:
        age="{root}/data/phenotypes/age_at_recruitment_21022.txt",
        pcs="{root}/data/phenotypes/genetic_PC_22009.txt"
    output:
        "{root}/output/run_gwas/covars/{dataset}/{subdataset}/control_{contrasts}_{chr}-PC{pc}.txt"
    params:
        pcNum = lambda wildcards: get_chr_num(wildcards.pc)
    shell:
        """
        Rscript code/run_gwas/assemble_quant_covar_control.R {input.age} {input.pcs} {params.pcNum} {output}
        """

rule run_fastGWA:
    input:
        pheno="{root}/data/phenotypes/StandingHeight_Normalized_50.txt",
        sp="/scratch/jgblanc/ukbb/grm/{gwas}/sparseGRM.grm.sp",
        id="/scratch/jgblanc/ukbb/grm/{gwas}/sparseGRM.grm.id",
        qcovar="{root}/output/run_gwas/covars/{dataset}/{subdataset}/{covar}_{contrasts}_{chr}-PC{pc}.txt",
        gp_genos="/scratch/jgblanc/ukbb/plink2-files/{gwas}/ukb_imp_chr{chr}_v3.pgen",
        overlap_snps="{root}/data/{dataset}/variants/{subdataset}/{gwas}/overlappingSNPs_chr{chr}.txt",
        covar="{root}/output/run_gwas/covars/fixed.txt"
    output:
        "{root}/output/run_gwas/{dataset}/{subdataset}/raw/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA",
    params:
        prefix_out = "{root}/output/run_gwas/{dataset}/{subdataset}/raw/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}",
        prefix_grm = "/scratch/jgblanc/ukbb/grm/{gwas}/sparseGRM",
        prefix_plink = "/scratch/jgblanc/ukbb/plink2-files/{gwas}/ukb_imp_chr{chr}_v3"
    threads: 16
    resources:
        mem_mb=100,
        time="00:30:00"
    shell:
        """
        gcta  --pheno {input.pheno} \
	--qcovar {input.qcovar} \
	--covar {input.covar} \
	--extract {input.overlap_snps} \
	--fastGWA-mlm --h2-limit 2.5 --maf 0 --geno 1 \
	--grm-sparse {params.prefix_grm} --pfile {params.prefix_plink} \
	--threads {threads} \
	--out {params.prefix_out}
        """

## Run Vilma

rule format_summary_stats:
    input:
        ss="{root}/output/run_gwas/{dataset}/{subdataset}/raw/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA",
        snps="/scratch/jgblanc/wb_high_info/varlist.txt.gz"
    output:
        "{root}/output/run_gwas/{dataset}/{subdataset}/vilma_format/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA"
    shell:
        """
        Rscript code/run_gwas/format_vilma.R {input.ss} {input.snps} {output}
        """

rule run_vilma:
    input:
        ss="{root}/output/run_gwas/{dataset}/{subdataset}/vilma_format/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}.fastGWA",
        ld="/scratch/jgblanc/wb_high_info/ld_manifest.txt"
    output:
        outlog = "{root}/output/prs/{dataset}/{subdataset}/vilma/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}.log",
        cov = "{root}/output/prs/{dataset}/{subdataset}/vilma/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}.covariance.pkl",
        npz = "{root}/output/prs/{dataset}/{subdataset}/vilma/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}.npz",
        estimates = "{root}/output/prs/{dataset}/{subdataset}/vilma/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}.estimates.tsv"
    params:
        prefix_out = "{root}/output/prs/{dataset}/{subdataset}/vilma/{gwas}/{covar}_{contrasts}_{chr}-PC{pc}"
    threads: 16
    resources:
        mem_mb=10000,
        time="24:00:00"
    shell:
        """
        vilma fit --logfile {output.outlog} \
	      --sumstats {input.ss} \
	      --output {params.prefix_out} \
	      --ld-schema {input.ld} \
	      -K 81 \
	      --ldthresh 0.8 \
	      --init-hg 0.5 \
	      --samplesizes 400e3 \
	      --names ukbb \
	      --learn-scaling \
	      --extract {input.ss}
        """


